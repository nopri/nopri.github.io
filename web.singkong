#
    web
    web.singkong

    Part of Singkong Programming Language Interpreter
    (c) Noprianto <nop@noprianto.com>

    More information: https://nopri.github.io/singkong.html
    Download Singkong: https://nopri.github.io/Singkong.jar
    (License: Free to use or redistribute, no warranty)

    To use this module:
    load_module("web")
;

require(11.3)

load_module("json")

var session_file_get_ = fn(d, n) 
"
[web] function: session_file_get_: returns file-based session data.
arguments: 2: STRING (directory) and STRING (cookie name)
return value: HASH (empty HASH on error)
" 
{
    var res = [{}]

    if (!is(d, "STRING") | !is(n, "STRING")) {
        return res[0]
    }

    var en = env()
    var cookie = en["HTTP_COOKIE"]
    if (cookie == null) {
        return res[0]
    }

    var cookies = split(cookie, ";")
    each(cookies, fn(e, i) {
        var temp = split(trim(e), "=")
        if (temp[0] == n) {
            var v = trim(temp[1])
            var f = d + separator() + v + ".txt"
            var f = abs(f)
            var o = read(f)
            if (o != null) {
                var j = json_parse(o)
                if (is(j, "HASH")) {
                    set(j, "session", v)
                    set(res, 0, j)
                }
            }
        }    
    })
    return res[0]
}

var session_file_get = fn(d) 
"
[web] function: session_file_get: returns file-based session data (using default cookie name: session).
arguments: 1: STRING (directory)
return value: HASH (empty HASH on error)
" 
{
    return session_file_get_(d, "session")
}

var session_new = fn() 
"
[web] function: session_new: creates new session data.
arguments: 
return value: HASH (empty HASH on error)
" 
{
    var v = md5(
        @ + "-" + 
        random() + "-" + 
        random_string(16, 16) + "-" + 
        uuid()
    )
    if (v == null) {
        return {}
    }
    return {
        "session": v
    }
}

var session_file_set = fn(d, s) 
"
[web] function: session_file_set: updates file-based session data.
arguments: 2: STRING (directory) and HASH (session data)
return value: BOOLEAN (write function) or NULL (error)
" 
{
    if (!is(d, "STRING") | !is(s, "HASH")) {
        return null
    }

    var v = s["session"]
    if (v == null) {
        return null
    }

    var v = trim(v)

    if (!isalnum(v)) {
        return null
    }

    set(s, "timestamp", number(@))
    set(s, "_type", "file")
    set(s, "_table", "")

    var f = d + separator() + v + ".txt"
    var f = abs(f)

    var j = json_string(s)
    return write(f, j)
}

var session_file_delete = fn(d, s) 
"
[web] function: session_file_delete: deletes file-based session data.
arguments: 2: STRING (directory) and HASH (session data)
return value: BOOLEAN (delete function) or NULL (error)
" 
{
    if (!is(d, "STRING") | !is(s, "HASH")) {
        return null
    }

    var v = s["session"]
    if (v == null) {
        return null
    }
    
    var v = trim(v)

    if (!isalnum(v)) {
        return null
    }

    var f = d + separator() + v + ".txt"
    var f = abs(f)
    if (stat(f)["exists"] == false) {
        return null
    }

    return delete(f)
}

var header_session_ = fn(n, p, s) 
"
[web] function: header_session_: creates a Set-Cookie header.
arguments: 3: STRING (cookie name), ARRAY (parameter), and HASH (session data)
return value: HASH (empty HASH on error)
" 
{
    if (
        !is(n, "STRING") | 
        !is(p, "ARRAY") | 
        !is(s, "HASH")
        ) {
            return {}
    }    
    
    var v = s["session"]
    if (v == null) {
        return {}
    }

    var a = array_extend([
        n + "=" + v
    ], p)
    
    return {
        "Set-Cookie": join(";", a)  
    }
}

var header_session = fn(s) 
"
[web] function: header_session: creates a Set-Cookie header (default, using default cookie name: session).
arguments: 1: HASH (session data)
return value: HASH (empty HASH on error)
" 
{
    return header_session_(
        "session", 
        [
        ],
        s
    )
}

var header_session_secure = fn(s) 
"
[web] function: header_session_secure: creates a Set-Cookie header (Secure, HttpOnly, using default cookie name: session).
arguments: 1: HASH (session data)
return value: HASH (empty HASH on error)
" 
{
    return header_session_(
        "session", 
        [
            "Secure",
            "HttpOnly"
        ],
        s
    )
}

var header_session_delete_ = fn(n, p, s) 
"
[web] function: header_session_delete_: deletes a cookie (creates a Set-Cookie header with Max-Age=0).
arguments: 3: STRING (cookie name), ARRAY (parameter), and HASH (session data)
return value: HASH (empty HASH on error)
" 
{
    if (
        !is(n, "STRING") | 
        !is(p, "ARRAY") | 
        !is(s, "HASH")
        ) {
            return {}
    }    
    
    var v = s["session"]
    if (v == null) {
        return {}
    }

    var a = array_extend([
        n + "=logout",
        "Max-Age=0"
    ], p)
    
    return {
        "Set-Cookie": join(";", a)  
    }
}

var header_session_delete = fn(s) 
"
[web] function: header_session_delete: deletes a cookie (creates a Set-Cookie header with Max-Age=0, using default cookie name: session).
arguments: 1: HASH (session data)
return value: HASH (empty HASH on error)
" 
{
    return header_session_delete_(
        "session", 
        [
        ],
        s
    )
}

var header_location = fn(r) 
"
[web] function: header_location: creates a Location header.
arguments: 1: STRING (location)
return value: HASH (empty HASH on error)
" 
{
    if (!is(r, "STRING")) {
        return {}
    }
    return {"Location": r}
}

var request_method = fn() 
"
[web] function: request_method: returns request method.
arguments: 
return value: STRING (in uppercase) or NULL (error)
" 
{
    var m = env()["REQUEST_METHOD"]
    if (m == null) {
        return null
    }
    return upper(string(m))
}

var query_string_hash = fn(h) 
"
[web] function: query_string_hash: returns URL-encoded query string from HASH.
arguments: 1: HASH
return value: STRING (empty: error)
" 
{
    if (!is(h, "HASH")) {
        return ""
    }
    var r = []
    var k = keys(h)
    each(k, fn(e, i) {
        var kk = url_encode(string(e))
        var v = h[e]
        var vv = url_encode(string(v))
        var q = kk + "=" + vv
        r + q
    })
    return join("&", r)
}

var query_string_array = fn(a) 
"
[web] function: query_string_array: returns URL-encoded query string from ARRAY (of ARRAY).
arguments: 1: ARRAY (of [key, value] or [key])
return value: STRING (empty: error)
" 
{
    if (!is(a, "ARRAY")) {
        return ""
    }
    if (!is_array_of(a, "ARRAY")) { 
        return ""       
    }
    var r = []
    each(a, fn(e, i) {
        if (len(e) > 0) {
            var kk = url_encode(string(e[0]))
            var vv = ""
            if (len(e) > 1) {
                var vv = url_encode(string(e[1]))
            }
            var q = kk + "=" + vv
            r + q
        }
    })
    return join("&", r)
}

var session_db_get_ = fn(d, t, n) 
"
[web] function: session_db_get_: returns database-based session data.
arguments: 3: DATABASE, STRING (table name; will be automatically created; fields: session_id char(128) not null primary key, contents clob / text, updated timestamp), and STRING (cookie name)
return value: HASH (empty HASH on error)
" 
{
    var res = [{}]

    if (!is(d, "DATABASE") | !is(t, "STRING") | !is(n, "STRING")) {
        return res[0]
    }

    var t = trim(t)
    if (empty(t)) {
        return res[0]
    }

    if (!isalnum(t)) {
        return res[0]
    }
    
    var sql_create = fn(t, c) {
        var ret = "
            create table 
            " + t + "
                (
                    session_id char(128) 
                        not null primary key,
                    contents " + c + ",
                    updated timestamp
                )
            "
        return ret
    }

    var en = env()
    var cookie = en["HTTP_COOKIE"]
    if (cookie == null) {
        return res[0]
    }

    var cookies = split(cookie, ";")
    each(cookies, fn(e, i) {
        var temp = split(trim(e), "=")
        if (temp[0] == n) {
            var v = trim(temp[1])
            var q = "
                    select
                        contents
                    from
                    " + t + "
                    where
                        session_id=?
                    "
            var r = query(d, [[q, [v]]])
            if (r == null) {
                var rr = query(d, [[sql_create(t, "clob"), []]])
                if (rr == null) {
                    var rr = query(d, [[sql_create(t, "text"), []]])
                }
                var r = query(d, [[q, [v]]])
            }
            var o = null
            if (r != null) {
                var r = r[0]
                if (!empty(r)) {
                    var r = r[0]
                    var o = r[0]
                }
            }
            if (o != null) {
                var j = json_parse(o)
                if (is(j, "HASH")) {
                    set(j, "session", v)
                    set(res, 0, j)
                }
            }
        }    
    })
    return res[0]
}

var session_db_get = fn(d) 
"
[web] function: session_db_get: returns database-based session data (using default table (session; will be automatically created; fields: session_id char(128) not null primary key, contents clob / text, updated timestamp) and cookie name (session)).
arguments: 1: DATABASE
return value: HASH (empty HASH on error)
" 
{
    return session_db_get_(d, "session", "session")
}

var session_db_set_ = fn(d, t, s) 
"
[web] function: session_db_set_: updates database-based session data.
arguments: 3: DATABASE, STRING (table name; will be automatically created; fields: session_id char(128) not null primary key, contents clob / text, updated timestamp), and HASH (session data).
return value: BOOLEAN or NULL (error)
" 
{
    if (!is(d, "DATABASE") | !is(t, "STRING") | !is(s, "HASH")) {
        return null
    }
    
    var t = trim(t)
    if (empty(t)) {
        return null
    }

    if (!isalnum(t)) {
        return null
    }

    var v = s["session"]
    if (v == null) {
        return null
    }

    var v = trim(v)

    if (!isalnum(v)) {
        return null
    }

    set(s, "timestamp", number(@))
    set(s, "_type", "database")
    set(s, "_table", t)

    var j = json_string(s)
   
    var r = query(d, [
            [
                "select * from " + t + "
                where
                    session_id=?
                ",
                [v]
            ]
        ]
    )
    if (r != null) {
        var r = r[0]
        var rr = null
        if (empty(r)) {
            var rr = query(d, [
                    [
                        "insert into " + t + "
                        ( 
                            session_id,
                            contents,
                            updated
                        )
                        values(
                            ?,
                            ?,
                            ?
                        )
                        ",
                        [v, j, @]
                    ]
                ]
            )
        } else {
            var rr = query(d, [
                    [
                        "update " + t + "
                        set 
                            contents=?,
                            updated=?
                        where
                            session_id=?
                        ",
                        [j, @, v]
                    ]
                ]
            )
        }
        if (rr == null) {
            return null
        }
        var rr = rr[0]
        return rr > 0
    }
    return null
}

var session_db_set = fn(d, s) 
"
[web] function: session_db_set: updates database-based session data (using default table (session; will be automatically created; fields: session_id char(128) not null primary key, contents clob / text, updated timestamp).
arguments: 2: DATABASE and HASH (session data).
return value: BOOLEAN or NULL (error)
" 
{
    return session_db_set_(d, "session", s)
}

var session_db_delete_ = fn(d, t, s) 
"
[web] function: session_db_delete_: deletes database-based session data.
arguments: 3: DATABASE, STRING (table name; will be automatically created; fields: session_id char(128) not null primary key, contents clob / text, updated timestamp), and HASH (session data).
return value: BOOLEAN or NULL (error)
" 
{
    if (!is(d, "DATABASE") | !is(t, "STRING") | !is(s, "HASH")) {
        return null
    }
    
    var t = trim(t)
    if (empty(t)) {
        return null
    }

    if (!isalnum(t)) {
        return null
    }

    var v = s["session"]
    if (v == null) {
        return null
    }
    
    var v = trim(v)

    if (!isalnum(v)) {
        return null
    }

    var r = query(d, [
            [
                "delete from " + t + "
                where
                    session_id=?
                ",
                [v]
            ]
        ]
    )
    if (r == null) {
        return null
    }
    var r = r[0]
    return r > 0 
}


var session_db_delete = fn(d, s) 
"
[web] function: session_db_delete: deletes database-based session data (using default table (session; will be automatically created; fields: session_id char(128) not null primary key, contents clob / text, updated timestamp).
arguments: 2: DATABASE and HASH (session data).
return value: BOOLEAN or NULL (error)
" 
{
    return session_db_delete_(d, "session", s)
}

var redirect_if = fn(c, u) 
"
[web] function: redirect_if: redirects to the specified URL when the specified condition is met.
arguments: 2: BOOLEAN (condition) and STRING (URL)
return value: NULL
" 
{
    if (!is(c, "BOOLEAN") | !is(u, "STRING")) {
        return null
    }

    if (c == true) {
        cgi_header(
            header_location(u)
        )
        exit()
    }
}

var redirect_to = fn(u) 
"
[web] function: redirect_to: redirects to the specified URL.
arguments: 1: STRING (URL)
return value: NULL
" 
{
    if (!is(u, "STRING")) {
        return null
    }

    cgi_header(
        header_location(u)
    )
    exit()
}


